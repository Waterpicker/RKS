plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    version = '1.2.2'
    group = 'com.thepokecraftmod'
    project.ext.lwjglVersion = "3.3.2"
    project.ext.jomlVersion = "1.10.5"
    project.ext.lwjglNatives = "natives-windows"

    configurations {
        implementation.extendsFrom shadow
    }

    repositories {
        mavenCentral()
        maven() { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        implementation "org.joml:joml:${jomlVersion}"
        implementation "org.lwjgl:lwjgl"
        implementation 'org.jetbrains:annotations:24.0.1'
        implementation 'org.slf4j:slf4j-api:2.0.7'
        implementation "com.thebombzen:jxlatte:1.0.6"
        implementation 'com.google.flatbuffers:flatbuffers-java:23.3.3'

        runtimeOnly 'org.slf4j:slf4j-simple:2.0.7'
        runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    }

    publishing {
        publications {
            myLib(MavenPublication) {
                from components.java
            }
        }

        repositories {
            mavenLocal()
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    tasks.build {
        dependsOn(shadowJar)
    }

    java {
        withSourcesJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}
